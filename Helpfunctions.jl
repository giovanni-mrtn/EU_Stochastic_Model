using Printf, Crayons, PrettyTables, AxisArrays, UnPack, Plots, StatsPlots, Colors, DataFrames

#//////////////////////////////////////////////////////////////////////////////////////////////////
# Struct containing all tunable model options and scenario settings used throughout the scripts.
# Fields include solver settings and scenario choices (e.g. `stochastic_model_scenarios`, `time_step`, `transmission_grid`).
Base.@kwdef mutable struct TModelParameters
    carbon_cap :: Float32
    storage:: Bool
    transmission:: Bool
    transmission_constrained :: Bool
    transmission_grid :: String
    nuclear:: Bool
    input_data_length :: Int32
    time_step ::Int32
    stochastic_model_scenarios  :: Vector{Int32}
    scenario_probability :: Vector{Float64}
    load_shedding_cost ::Float32
    loss_of_load_penalty :: Float32 
    load_shedding_ratio :: Float32
    use_mean_aggregation :: Bool
    planned_outage_rate ::Float32
    solve_stochastic_model :: Bool
    simulate_results :: Bool
    fix_hydro:: Bool
end

#//////////////////////////////////////////////////////////////////////////////////////////////////
"""
GetTableSummary(problem, input, scenario_data, solution)

Create human-readable tables from the optimization solution. Returns:
- `tables`: array of tuples suitable for `SaveAllTables`
- `PlantGeneration`, `InstalledCapacity`, `Electricity_r`: arrays useful for plotting
"""
function GetTableSummary(problem, input, scenario_data, solution)       # Summarize model solution into tables

    @unpack storage, transmission, nuclear, time_step, load_shedding_cost, planned_outage_rate = problem 
    @unpack REGION, TECH, PERIOD, no_regions, no_techs, no_periods, valid_links = input
    @unpack Cost_r, Capacity_r, Electricity_r, TransmissionCapacity_r, TotalEmissions_r, TotalSheddedLoadCost_r, OperationalCost_r, FixedCost_r, InvestmentCost_r =solution
    @unpack SCENARIO, no_scenarios, load, cf, prob = scenario_data

    TransmissionCapacity = AxisArray(zeros(no_regions, no_regions), REGION, REGION)
    no_plants = length(TECH)
    for r in REGION
        for r1 in REGION
            if r1 > r && valid_links[r, r1]
                TransmissionCapacity[r, r1] = TransmissionCapacity_r[r, r1]/1e3 # GW
            else
                TransmissionCapacity[r, r1] = 0
            end
        end
    end 
    
    PlantGeneration = AxisArray(zeros(no_scenarios, no_plants), SCENARIO, TECH)
    PlantUtilization = AxisArray(zeros(no_scenarios, no_plants), SCENARIO, TECH)

    AveragePlantGeneration = AxisArray(zeros(no_plants), TECH)
    AveragePlantUtilization = AxisArray(zeros(no_plants), TECH)

    for s in SCENARIO
        for p in TECH
            PlantGeneration[s, p] = sum(Electricity_r[s, :, p, :])/1e6
            total_capacity = sum(Capacity_r[r, p] for r in REGION)
            if total_capacity > 1e-3
                PlantUtilization[s, p] = 100 * sum(Electricity_r[s, :, p, :]) / (time_step * no_periods * total_capacity)
            else
                PlantUtilization[s, p] = 0
            end
        end
    end

    for p in TECH    
        AveragePlantGeneration[p] = sum(PlantGeneration[:, p].*prob)
        AveragePlantUtilization[p] = sum(PlantUtilization[:, p].*prob)
    end

    PlantGenerationData = vcat(PlantGeneration, AveragePlantGeneration')
    PlantUtilizationData = vcat(PlantUtilization, AveragePlantUtilization')

    PlantGenerationLabel = vcat("Scenario " .* string.(SCENARIO), "Average")

    InstalledCapacity = AxisArray(zeros(no_regions, no_techs), REGION, TECH)
    TotalInstalledCapacity = AxisArray(zeros(no_techs), TECH)
    for r in REGION
        for p in TECH
            InstalledCapacity[r, p] = Capacity_r[r, p]/1e3          
        end
    end

    for p in TECH
        TotalInstalledCapacity[p] = sum(InstalledCapacity[:, p]) 
    end

    InstalledCapacityData = vcat(InstalledCapacity, TotalInstalledCapacity');
    InstalledCapacityLabel = vcat(string.(REGION), "Total")

    
    SatisfiedLoad = AxisArray(zeros(no_scenarios), SCENARIO)
    SheddedLoad = AxisArray(zeros(no_scenarios), SCENARIO)
    UnitProductionCost = AxisArray(zeros(no_scenarios), SCENARIO)
    for s in SCENARIO
        SheddedLoad[s] = TotalSheddedLoadCost_r[s]/load_shedding_cost
        SatisfiedLoad[s] = (sum(load[s,:,:])*time_step) - SheddedLoad[s] 
        UnitProductionCost[s] = (InvestmentCost_r + OperationalCost_r[s] + FixedCost_r[s] + TotalSheddedLoadCost_r[s]) / SatisfiedLoad[s]
    end
 
    # Tables
    Total_costs_r = InvestmentCost_r/1e6 + sum(OperationalCost_r .* prob)/1e6 + sum(FixedCost_r .* prob)/1e6 + sum(TotalSheddedLoadCost_r .* prob)/1e6
    tempdata_1_vec = [Cost_r,
                  Total_costs_r,
                  InvestmentCost_r/1e6,
                  sum(FixedCost_r .* prob)/1e6,
                  sum(OperationalCost_r .* prob)/1e6,
                  sum(TotalEmissions_r .* prob)/1e6,
                  sum(TotalSheddedLoadCost_r .* prob)/1e6,
                  sum(SatisfiedLoad .* prob)/1e6,
                  sum(SheddedLoad .* prob)/1e6,
                  sum(UnitProductionCost .* prob)]
    
    templabel_1 = ["Objective function (M€)",
                    "Total costs (M€)",
                    "Invetment cost (M€)",
                    "Fixed cost (M€)",
                    "Operational cost (M€)",
                    "CO2 emissions (Mt)",
                    "Load shedding cost (M€)",
                    "Satisfied load (TWh)",
                    "Shedded Load (TWh)",
                    "Unit production cost (€/MWh)"]
    
    # Create the cost summary table by combining labels and values into a single DataFrame.
    # Use DataFrame to ensure robust handling by PrettyTables
    tempdata_1 = DataFrame(Row = templabel_1, Value = tempdata_1_vec)
    tempdata_2 =  [ (prob .*100)';
                    (FixedCost_r ./1e6)';
                    (OperationalCost_r ./1e6)';
                    (TotalEmissions_r ./1e6)';
                    (TotalSheddedLoadCost_r ./1e6)';
                    (SatisfiedLoad ./1e6)';
                    (SheddedLoad ./1e6)';
                    UnitProductionCost' ]
  
    templabel_2 = ["Probability (%)",
                      "Fixed cost (M€)",
                      "Operational cost (M€)",
                      "CO2 emissions (Mt)",
                      "Load shedding cost (M€)",
                      "Satisfied load (TWh)",
                      "Shedded Load (TWh)",
                      "Unit production cost (€/MWh)"]

    tables = [
        ("Cost Summary:", tempdata_1, ["Row", "Value"], []),
        ("Installed Capacity (GW): ", InstalledCapacityData, string.(TECH), InstalledCapacityLabel),
        ("Transmission Capacity (GW): ", TransmissionCapacity, string.(REGION), string.(REGION)),
        ("Generation (TWh): ", PlantGenerationData, string.(TECH), PlantGenerationLabel),
        ("Utilization (%): ", PlantUtilizationData, string.(TECH), PlantGenerationLabel),
        ("Scenario Summary: ", tempdata_2, "Scenario " .* string.(SCENARIO), templabel_2)
    ]  
    return (; tables, PlantGeneration, InstalledCapacity, Electricity_r)

end

#//////////////////////////////////////////////////////////////////////////////////////////////////
"""
SaveAllTables(filename, tables)

Save a list of tables (as produced by `GetTableSummary`) to an HTML file using `PrettyTables`.
- `filename` will be written to the current working directory with `.html` extension.
"""
function SaveAllTables(filename, tables)
    try 
        open(joinpath(pwd(), "$filename.html"), "w") do f
            # --- HTML header ---
            println(f, "<html><head><style>")
            println(f, "table {border-collapse: collapse; width: auto;}")
            println(f, "th, td {border: 1px solid #ddd; padding: 8px; text-align: left;}")
            println(f, "th {background-color: #f2f2f2; font-weight: bold;}")
            println(f, "</style></head><body>")

            for (table_name, table_data, table_headers, table_rownames) in tables
                println("$table_name")

                # Convert all tables to DataFrame for consistent handling
                if table_data isa DataFrame
                    df = table_data
                else
                    # Convert matrix/AxisArray to DataFrame
                    if isempty(table_rownames)
                        df = DataFrame(table_data, Symbol.(table_headers))
                    else
                        # Create DataFrame with row names as first column
                        df = DataFrame(table_data, Symbol.(table_headers))
                        # Use "Row" for tables starting from "Generation" onwards, otherwise "Region"
                        col_name = if occursin("Generation", table_name) || occursin("Utilization", table_name) || occursin("Scenario Summary", table_name) || occursin("Simulation Results", table_name)
                            :Row
                        else
                            :Region
                        end
                        insertcols!(df, 1, col_name => table_rownames)
                    end
                end

                # Format numeric columns to remove exponential notation
                for col in names(df)
                    if eltype(df[!, col]) <: Number
                        df[!, col] = map(x -> begin
                            rounded = round(x, digits=4)
                            # Format as string to avoid exponential notation
                            if abs(rounded) < 0.0001 && rounded != 0
                                @sprintf("%.4e", rounded)
                            else
                                rounded
                            end
                        end, df[!, col])
                    end
                end

                # Convert DataFrame to matrix to avoid showing type subheader
                headers = names(df)
                data_matrix = Matrix(df)

                # Print to console
                pretty_table(stdout, data_matrix)
                println("")  # spacing in console

                # Write to HTML file manually
                println(f, "<h3>$table_name</h3>")
                println(f, "<table>")
                println(f, "  <thead>")
                println(f, "    <tr>")
                for header in headers
                    println(f, "      <th style=\"font-weight: bold; text-align: right;\">$header</th>")
                end
                println(f, "    </tr>")
                println(f, "  </thead>")
                println(f, "  <tbody>")
                
                # Write data rows
                for i in 1:size(data_matrix, 1)
                    println(f, "    <tr>")
                    for j in 1:size(data_matrix, 2)
                        val = data_matrix[i, j]
                        println(f, "      <td style=\"text-align: right;\">$val</td>")
                    end
                    println(f, "    </tr>")
                end
                
                println(f, "  </tbody>")
                println(f, "</table>")
                println(f, "<br><br>")
            end

            println(f, "</body></html>")
        end

        println("All tables saved to: $(joinpath(pwd(), "$filename.html"))")

    catch e
        println("Error saving tables to file: $e")
    end
end

#//////////////////////////////////////////////////////////////////////////////////////////////////
#=
function PlotSolution(solution_summary, problem, input, scenario_data)
    @unpack time_step = problem
    @unpack REGION, TECH, PERIOD, no_regions, no_techs, no_periods = input
    @unpack SCENARIO, no_scenarios, load = scenario_data
    @unpack PlantGeneration, InstalledCapacity, Electricity_r = solution_summary

    # Define a color mapping for consistency across all plots
    color_map = Dict(
        :HydroRes => :dodgerblue3,
        :HydroRoR => :deepskyblue,
        :PSHopenTurb => :seagreen3,
        :PSHopenPump => :seagreen2,
        :PSHclosedTurb => :forestgreen,
        :PSHclosedPump => :limegreen,
        :Gas => :firebrick3,
        :Wind => :skyblue,
        :OffWind => :steelblue,
        :PV => :gold,
        :RooftopPV => :orange,
        :Battery => :darkorange1,
        :Inverter => :purple,
        :H2GT => :brown,
        :Electrolyzer => :tan,
        :Nuclear => :lime
    )

    gr()

    # --- Installed capacity (stacked by technology) ---
    x_regions = string.(REGION)
    tech_colors = [get(color_map, p, :gray) for p in TECH]

    groupedbar(x_regions, Matrix(InstalledCapacity),
        title="Installed capacity (GW)",
        ylabel="GW",
        bar_position = :stack,
        bar_width = 0.5,
        color = permutedims(tech_colors),
        labels = permutedims(string.(TECH)),
        tickfont=14, legendfont=12, guidefont=12, titlefont=14,
        legend = :outertopright, size = (1000, 500), automargin=true) |> display

    # --- Plant generation by scenario (stacked) ---
    SELECTEDPLANTS = [:HydroRes, :HydroRoR, :Gas, :Wind, :OffWind, :PV, :RooftopPV, :Inverter, :Nuclear]
    no_selected_plants = length(SELECTEDPLANTS)

    gen_matrix = Array(PlantGeneration[:, SELECTEDPLANTS])  # dimensions: scenarios x selected_plants (TWh)
    scen_labels = string.(SCENARIO)
    plant_colors = [get(color_map, p, :gray) for p in SELECTEDPLANTS]

    groupedbar(scen_labels, gen_matrix,
        title="Plant generation (TWh)",
        ylabel="TWh",
        bar_position = :stack,
        bar_width = 0.5,
        color = permutedims(plant_colors),
        labels = permutedims(string.(SELECTEDPLANTS)),
        tickfont=14, legendfont=12, guidefont=12, titlefont=14,
        legend = :outertopright, size = (1000, 500), automargin=true) |> display

    # --- Time series: stacked area of selected plant generation and load ---
    PERIODSCENARIO = 1:(no_periods * no_scenarios)
    total_periods = length(PERIODSCENARIO)
    production = zeros(Float64, no_selected_plants, total_periods)  # TWh per period
    TotalElecLoad = zeros(Float64, total_periods)  # TWh per period

    for s in SCENARIO
        for h in PERIOD
            index = (s - 1) * no_periods + h
            for (i, p) in enumerate(SELECTEDPLANTS)
                production[i, index] = sum(Electricity_r[s, :, p, h]) / 1e6  # Convert MWh -> TWh
            end
            TotalElecLoad[index] = sum(load[s, REGION, h]) * time_step / 1e6  # TWh
        end
    end

    x = PERIODSCENARIO
    # Plot stacked area (use seriestype=:area with stack=true)
    areaplot(x, production', labels = permutedims(string.(SELECTEDPLANTS)), color = permutedims(plant_colors), fillalpha = 0.95,
         xlabel="Period", ylabel="TWh", title="Time series generation (stacked)", legend = :outertopright,
         tickfont=12, legendfont=12, guidefont=14, titlefont=16, size=(1500,500), automargin=true)

    # Overlay load as a line
    plot!(x, TotalElecLoad, linewidth=2.5, color=:black, linestyle=:solid, label="Load") |> display
end
=#